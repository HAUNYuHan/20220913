
```
/*********
  ESP32 WebSocket Server: Control Outputs (Arduino IDE)
  https://RandomNerdTutorials.com/esp32-websocket-server-arduino/
  In this tutorial you’ll learn how to build a web server with the ESP32 using WebSocket communication protocol. 
  As an example, we’ll show you how to build a web page to control the ESP32 outputs remotely. 
  The output state is displayed on the web page and it updates automatically in all clients.
  The program is based ESPAsyncWebServer
  
*********/

// Import required libraries
#include <WiFi.h>
#include <AsyncTCP.h>
#include <ESPAsyncWebServer.h>
#include <SPIFFS.h>

// Replace with your network credentials
const char* ssid = "LAB_I3301LAB_I3301";//"LAB_I3301"; // "I2313";
const char* password = "bdt4fc20Zcbdt4fc20Zc";//"bdt4fC2oZc"; //"0928474616";

// when the current GPIO state for a change occurs on the GPIO state, the interface is updated instantaneously
bool ledState = 0;
//  web controls GPIO 2 – the on-board LED
const int ledPin = 2;

// Create AsyncWebServer object on port 80
AsyncWebServer server(80);
// The ESPAsyncWebServer library includes a WebSocket plugin
// Create an AsyncWebSocket object: ws to handle the connections on the "/ws" path in the js inside of html.
AsyncWebSocket ws("/ws");

// generate dynamic html content
const char index_html[] PROGMEM = R"rawliteral(
  <!DOCTYPE HTML>
  <html>
      <head>
          <title>ESP Web Server</title>
          <meta name="viewport" content="width=device-width, initial-scale=1">
          <link rel="icon" href="data:,">
          
          <style>
              /* css tag*/
              html {
                  font-family: Arial, Helvetica, sans-serif;
                  text-align: center;
              }
              h1 {
                  font-size: 1.8rem;
                  color: white;
              }
              h2 {
                  font-size: 1.5rem;
                  font-weight: bold;
                  color: #143642;
              }
              body {
                  margin: 0;
              }
                
            /* css class ID*/
            .topnav {
              overflow: hidden;
              background-color: #143642;
            }
            .content {
              padding: 30px;
              max-width: 600px;
              margin: 0 auto;
            }
            .card {
              background-color: #F8F7F9;;
              box-shadow: 2px 2px 12px 1px rgba(140,140,140,.5);
              padding-top:10px;
              padding-bottom:20px;
            }
            .button {
              padding: 15px 50px;
              font-size: 24px;
              text-align: center;
              outline: none;
              color: #fff;
              background-color: #0f8b8d;
              border: none;
              border-radius: 5px;
              -webkit-touch-callout: none;
              -webkit-user-select: none;
              -khtml-user-select: none;
              -moz-user-select: none;
              -ms-user-select: none;
              user-select: none;
              -webkit-tap-highlight-color: rgba(0,0,0,0);
             }
             .button:active {
               background-color: #0f8b8d;
               box-shadow: 2 2px #CDCDCD;
               transform: translateY(2px);
             }
             .state {
               font-size: 1.5rem;
               color:#8c8c8c;
               font-weight: bold;
             }
          </style>   
      </head>
  <body>
    <div class="topnav">
      <h1>ESP WebSocket Server</h1>
    </div>
    <div class="content">
      <div class="card">
        <h2>Output - GPIO 2</h2>
        <p class="state">state: <span id="state">%STATE%</span></p>
        <p><button id="button" class="button">Toggle</button></p>
      </div>
    </div>
      
      <!-- js script to handle WebSocket protocol-->
      <!--initializing a WebSocket connection with the server as soon the web interface is fully loaded in the browser and handling data exchange through WebSockets.-->
  
      <script>
        // Open the entry point to the WebSocket interface.
        // gets the current page address (the web server IP address).
        var gateway = `ws://${window.location.hostname}/ws`;
        var websocket;
        
        // a window listener for event and trigger the callback function to initialize a WebSocket connection with the server and add event listeners to the buttons.
        window.addEventListener('load', onLoad);
        // callback function to open a WebSocket connection and add 
        // onClick event listener
        function onLoad(event) {
          initWebSocket(); // open a WebSocket connection
          initButton();    // add an event listener to handle onClick 
        }
          
        function initWebSocket() {
          console.log('From the web client trying to open a WebSocket connection by the WebSocket protocol...');
          // open a WebSocket connection with the ESP32 at the gateway path  
          websocket = new WebSocket(gateway);
          websocket.onopen    = onOpen;
          websocket.onclose   = onClose;
          websocket.onmessage = onMessage; // <-- add this line
        }
        
        function onOpen(event) {
          console.log('WebSocket connection is opened.');
          websocket.send('Hello from the web client!');
        }
  
        function onClose(event) {
          // some reason the web socket connection is closed, we call the initWebSocket() function again after 2000 milliseconds (2 seconds).  
          console.log('WebSocket connection is closed.');
          console.log('The WebSocket will be initialized after 2 seconds...');  
          setTimeout(initWebSocket, 2000);
        }
        
        // ESP32 server sends "1" or "0" and js receives that information via WebSocket protocol. 
        // i.e. ws.textAll() or client->text()
        function onMessage(event) {
          console.log('Received event: ');
          console.log(event); // event can be viewed from the Google console log
          var state;
          if (event.data == "1"){
            state = "ON";
          }else{
            state = "OFF";
          }
          // handles what to do with the information received to update the state accordingly.  
          document.getElementById('state').innerHTML = state;
        }
        
        // a callback function to start an onClick event listener that trigger the toggle() function
        function initButton() {
          document.getElementById('button').addEventListener('click', toggle);
        }
        // the toggle function is called.
        function toggle(){
          websocket.send('toggle');
        }
      </script>
  </body>
  </html>
)rawliteral";

void setup(){
  // Serial port for debugging purposes
  Serial.begin(115200);

  pinMode(ledPin, OUTPUT);
  digitalWrite(ledPin, LOW);
  
  // Connect to Wi-Fi
  WiFi.begin(ssid, password);
  // check wifi connection
  while (WiFi.status() != WL_CONNECTED) {
    delay(1000);
    Serial.println("Connecting to WiFi..");
  }

  // Print ESP Local IP Address
  Serial.println(WiFi.localIP());

  // initializes a WebSocket connection on the gateway 
  initWebSocket();

  // Register the route for root "/" web page with template processor() function
  server.on("/", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send_P(200, "text/html", index_html, processor);
  });

  // Start server
  server.begin();
}

void loop() {
  ws.cleanupClients();
  digitalWrite(ledPin, ledState);
}

//  initializes a WebSocket connection on the gateway
void initWebSocket() {
  ws.onEvent(onEvent); // the websocket events handling function
  server.addHandler(&ws); // 
}

// the websocket events handling function
void onEvent(AsyncWebSocket *server, AsyncWebSocketClient *client, AwsEventType type,
             void *arg, uint8_t *data, size_t len) {
  //  client: the object we will use to send data back to the client, when it connects to our server.
  //  type: an enumerated value. This enum contains all the websocket events that can trigger the execution of our handling function.
  // we use this third argument to check what event has occurred and handle it accordingly.
            
  switch (type) {
    // the event corresponds to the connection of a client
    // we have access to a pointer to the client object, then we need to use the '->' 
    // operator in order to access the text/id/remoteIP methods on our client object
    case WS_EVT_CONNECT:
      client->text("Hello from ESP32 Server!"); // appear on console in terms of websock.onmessage
      Serial.printf("WebSocket client #%u connected from %s\n", client->id(), client->remoteIP().toString().c_str());
      break;
    //   
    case WS_EVT_DISCONNECT:
      Serial.printf("WebSocket client #%u disconnected\n", client->id());
      break;
    case WS_EVT_DATA:
      handleWebSocketMessage(arg, data, len);
      break;
    case WS_EVT_PONG:
    case WS_EVT_ERROR:
      break;
  }
}

void handleWebSocketMessage(void *arg, uint8_t *data, size_t len) {
  AwsFrameInfo *info = (AwsFrameInfo*)arg;
  data[len] = 0;
  Serial.println(String() + "Received data from client witout conditions: " + (char*)data);
  
  if (info->final && info->index == 0 && info->len == len && info->opcode == WS_TEXT) {
    if (strcmp((char*)data, "toggle") == 0) {
      Serial.println(String() + "Received data from client filtered with conditions: " + (char*)data);
      ledState = !ledState;
      // notify all clients by calling the notifyClients() function. 
      // This way, all clients are notified of the change and update the interface accordingly.
      notifyClients();
    }
  }
}

// notifies all clients with a message containing whatever you pass as a argument. 
void notifyClients() {
  // In this case, we’ll want to notify all clients (inc. the web) of the current LED state whenever there’s a change.
  // event is received by websocket.onmessage() on the js of the web.
  ws.textAll(String(ledState));
  //ws.textAll("New event!");
}

// The processor() function is responsible for searching for placeholders on the HTML text and 
// replace them with whatever we want before sending the web page to the browser. 
String processor(const String& var){
  Serial.println(var);
  // check the placehold on the web
  if(var == "STATE"){
    if (ledState){
      return "ON";
    }
    else{
      return "OFF";
    }
  }
  return String();
}
```
